new
  rl(`rho:registry:lookup`), RhoSpecCh,
  stdlog(`rho:io:stdlog`),
  test_get_before_set,
  test_get_after_set,
  test_get_after_update_nil,
  test_get_after_update,
  test_fast_unsafe_get_after_set,
  test_contains_before_set,
  test_contains_after_set,
  test_get_after_set_to_nil,
  test_contains_after_set_to_nil,
  test_contention,
  test_depth_matches_init,
  test_get_or_else_when_key_missing,
  test_get_or_else_when_key_present,
  test_delete_key,
  test_delete_missing_key
in {
  rl!(`rho:id:zphjgsfy13h1k85isc8rtwtgt3t9zzt5pjd5ihykfmyapfc4wt3x5h`, *RhoSpecCh) |
  for(@(_, RhoSpec) <- RhoSpecCh) {
    @RhoSpec!("testSuite",
      [
        ("Get before set returns Nil", *test_get_before_set),
        ("Get after set returns the new value", *test_get_after_set),
        ("Get after updating a nonexistent key should return Nil", *test_get_after_update_nil),
        ("Get after updating an existing key should return the right value", *test_get_after_update),
        ("Fast unsafe get after set returns the new value", *test_fast_unsafe_get_after_set),
        ("Contains before set returns false", *test_contains_before_set),
        ("Contains after set returns true", *test_contains_after_set),
        ("Get after set to Nil returns Nil", *test_get_after_set_to_nil),
        ("Contains after set to Nil returns true", *test_contains_after_set_to_nil),
        ("Works under contention", *test_contention),
        ("Depth matches init", *test_depth_matches_init),
        ("GetOrElse when key missing", *test_get_or_else_when_key_missing),
        ("GetOrElse when key present", *test_get_or_else_when_key_present),
        ("Delete key", *test_delete_key),
        ("Delete missing key", *test_delete_missing_key)
      ])
  } |

  new TreeHashMapCh in {
    rl!(`rho:lang:treeHashMap`, *TreeHashMapCh) |
    for(TreeHashMap <- TreeHashMapCh) {
      contract test_get_before_set(rhoSpec, _, ackCh) = {
        new ret, ch in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("get", thm, "no such key", *ch) |
            rhoSpec!("assert", (Nil, "== <-", *ch), "Getting with an unused key returns Nil", *ackCh)
          }
        }
      } |

      contract test_get_after_update_nil(rhoSpec, _, ackCh) = {
        new thmCh, ack, ch, update in {
          TreeHashMap!("init", 3, *thmCh) |
          contract update(@val, ret) = {
            ret!(val + 1)
          } |
          for (@thm <- thmCh) {
            TreeHashMap!("update", thm, "no such key", *update, *ack) |
            for (_ <- ack) {
              TreeHashMap!("get", thm, "no such key", *ch) |
              rhoSpec!("assert", (Nil, "== <-", *ch), "Getting after updating with an unused key returns Nil", *ackCh)
            }
          }
        }
      } |

      contract test_get_after_update(rhoSpec, _, ackCh) = {
        new thmCh, ack1, ack2, ch, update in {
          TreeHashMap!("init", 3, *thmCh) |
          contract update(@val, ret) = {
            ret!(val + 1)
          } |
          for (@thm <- thmCh) {
            TreeHashMap!("set", thm, "key", 0, *ack1) |
            for (_ <- ack1) {
              TreeHashMap!("update", thm, "key", *update, *ack2) |
              for (_ <- ack2) {
                TreeHashMap!("get", thm, "key", *ch) |
                rhoSpec!("assert", (1, "== <-", *ch), "Getting after updating after setting returns the right value", *ackCh)
              }
            }
          }
        }
      } |

      contract test_get_after_set(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("get", thm, "some key", *ch1) |
              TreeHashMap!("get", thm, "some other key", *ch2) |
              rhoSpec!("assertMany",
                [
                  (("some val", "== <-", *ch1), "Getting with the proper key returns the correct value"),
                  ((Nil, "== <-", *ch2), "Getting with a different key returns Nil")
                ], *ackCh)
            }
          }
        }
      } |

      contract test_fast_unsafe_get_after_set(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("fastUnsafeGet", thm, "some key", *ch2) |
              rhoSpec!("assert", ("some val", "== <-", *ch2), "Getting with the proper key returns the correct value", *ackCh)
            }
          }
        }
      } |

      contract test_contains_before_set(rhoSpec, _, ackCh) = {
        new ret, ch in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("contains", thm, "no such key", *ch) |
            rhoSpec!("assert", (false, "== <-", *ch), "Getting with an unused key returns false", *ackCh)
          }
        }
      } |

      contract test_contains_after_set(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2, ch3 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("contains", thm, "some key", *ch2) |
              TreeHashMap!("contains", thm, "some other key", *ch3) |
              rhoSpec!("assertMany",
                [
                  ((true, "== <-", *ch2), "Getting with the proper key returns true"),
                  ((false, "== <-", *ch3), "Getting with a different key returns false")
                ], *ackCh)
            }
          }
        }
      } |

      contract test_get_after_set_to_nil(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", Nil, *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("get", thm, "some key", *ch2) |
              rhoSpec!("assert", (Nil, "== <-", *ch2), "Getting with the proper key returns the correct value", *ackCh)
            }
          }
        }
      } |

      contract test_contains_after_set_to_nil(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2, ch3 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", Nil, *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("contains", thm, "some key", *ch2) |
              TreeHashMap!("contains", thm, "some other key", *ch3) |
              rhoSpec!("assertMany",
                [
                  ((true, "== <-", *ch2), "Getting with the proper key returns true"),
                  ((false, "== <-", *ch3), "Getting with a different key returns false")
                ], *ackCh)
            }
          }
        }
      } |

      contract test_contention(rhoSpec, _, ackCh) = {
        new tryIt in {
          contract tryIt(@n, @flag) = {
            new out(`rho:io:stdlog`), mapCh in {
              TreeHashMap!("init", 3, *mapCh) |
              for (@map <- mapCh) {
                new ret1, ret2, ret3 in {
                  TreeHashMap!("set", map, "category", "object", *ret1) |
                  TreeHashMap!("set", map, "category", "morphism", *ret2) |
                  for (_ <- ret1 & _ <- ret2) {
                    TreeHashMap!("get", map, "category", *ret3) |
                    for (@val3 <- ret3) {
                      out!("info", [n, val3]) |
                      if (n == 9) {
                        rhoSpec!("assert", (flag, "==", true), "Placeholder", *ackCh)
                      } else {
                        tryIt!(n+1, flag and (val3 != Nil))
                      }
                    }
                  }
                }
              }
            }
          } |
          tryIt!(0, true)
        }
      } |

      contract test_depth_matches_init(rhoSpec, _, ackCh) = {
        new ret, ch in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("depth", thm, *ch) |
            rhoSpec!("assert", (3, "== <-", *ch), "Depth returns same value provided to init", *ackCh)
          }
        }
      } |

      contract test_get_or_else_when_key_missing(rhoSpec, _, ackCh) = {
        new ret, valueCh, nilCh in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("getOrElse", thm, 0, *valueCh, *nilCh) |
            rhoSpec!("assert", ("nothing <-", *nilCh), "nilCh is triggered when key is missing", *ackCh)
          }
        }
      } |

      contract test_get_or_else_when_key_present(rhoSpec, _, ackCh) = {
        new ret, ch1, valueCh, nilCh in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("getOrElse", thm, "some key", *valueCh, *nilCh) |
              rhoSpec!("assert", ("some val", "== <-", *valueCh), "valueCh is triggered when key is present", *ackCh)
            }
          }
        }
      } |

      contract test_delete_key(rhoSpec, _, ackCh) = {
        new ret, ch1, deleteCh, getAfterDeleteCh in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("delete", thm, "some key", *deleteCh) |
              for (@deleted <- deleteCh) {
                TreeHashMap!("get", thm, "some key", *getAfterDeleteCh) |
                rhoSpec!("assertMany",
                  [
                    (("some val", "==", deleted), "deleteCh contains deleted value"),
                    ((Nil, "== <-", *getAfterDeleteCh), "value is not present in map"),
                  ], *ackCh)
              }
            }
          }
        }
      } |

      contract test_delete_missing_key(rhoSpec, _, ackCh) = {
        new ret, deleteCh in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("delete", thm, "some key", *deleteCh) |
            rhoSpec!("assert", (Nil, "== <-", *deleteCh), "deleteCh contains Nil", *ackCh)
          }
        }
      }
    }
  }
}
